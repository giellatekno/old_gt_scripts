<?xml version="1.0"?>
<!--+
    | Script to check the info on parallelity of the XML corpus files from a given language (slang) to a given language (tlang). 
    | At the moment, it works on the files generated by collect_parallel_files.xsl.
    | The parallelity is checked from one language to the other, i.e., for each pair, the check ought to be done twice.
    | Usage: java -Xmx2048m net.sf.saxon.Transform -it main THIS_SCRIPT slang=SOURCE_LANG tlang=TARGET_LANG sfile=PATH_TO_FILE tfile=PATH_TO_FILE
    | Example:
    | java net.sf.saxon.Transform -it main check_parallelity.xsl slang=sme tlang=nob sfile=xyz/sme-nob_paco_summary.xml tfile=xyz/nob-sme_paco_summary.xml
    | java net.sf.saxon.Transform -it main check_parallelity.xsl slang=nob tlang=sme sfile=xyz/nob-sme_paco_summary.xml tfile=xyz/sme-nob_paco_summary.xml
    +-->

<xsl:stylesheet version="2.0"
		xmlns:xsl="http://www.w3.org/1999/XSL/Transform"
		xmlns:xs="http://www.w3.org/2001/XMLSchema"
		xmlns:local="nightbar"
		exclude-result-prefixes="xs local">

  <xsl:strip-space elements="*"/>

  <xsl:output method="text" name="txt"
	      encoding="UTF-8"
	      omit-xml-declaration="no"
	      indent="yes"/>

  <xsl:output method="xml" name="xml"
	      encoding="UTF-8"
	      omit-xml-declaration="no"
	      indent="yes"/>
  
  <!-- Input dir -->
  <xsl:param name="sfile" select="'default.xml'"/>
  <xsl:param name="tfile" select="'default.xml'"/>
  <xsl:param name="slang" select="'sme'"/>
  <xsl:param name="tlang" select="'nob'"/>
  
  <!-- Output dir and file -->
  <xsl:variable name="outDir" select="'para_check'"/>
  <xsl:variable name="outFile" select="'paracheck'"/>
  <xsl:variable name="outFormat" select="'xml'"/>
  <xsl:variable name="e" select="$outFormat"/>

  <xsl:template match="/" name="main">
    <xsl:variable name="para_files">
      <parallel_files>
	<xsl:for-each select="for $f in document($sfile)//file return $f">
	  <xsl:variable name="current_f_loc" select="./f_loc"/>
	  <xsl:variable name="current_f_name" select="./f_name"/>
	  <xsl:variable name="current_pf_loc" select="./pf_loc"/>
	  <xsl:variable name="current_pf_name" select="./pf_name"/>
	  <file>
	    <xsl:variable name="hit">
	      <sf>
		<xsl:value-of select="concat($current_f_loc, $current_f_name)"/>
	      </sf>
	      <tf>
		<xsl:value-of select="concat($current_pf_loc, $current_pf_name)"/>
	      </tf>
	    </xsl:variable>

	    <xsl:variable name="dit">
	      <sf>
		<xsl:value-of select="concat(document($tfile)//file[./f_name = $current_pf_name]/pf_loc,
				      document($tfile)//file[./f_name = $current_pf_name]/pf_name)"/>
	      </sf>
	      <tf>
		<xsl:value-of select="concat(document($tfile)//file[./f_name = $current_pf_name]/f_loc,
				      document($tfile)//file[./f_name = $current_pf_name]/f_name)"/>
	      </tf>
	    </xsl:variable>
	    <xsl:attribute name="parallelity">
	      <xsl:value-of select="$hit = $dit"/>
	    </xsl:attribute>
	    <xsl:if test="not($hit = $dit)">
	      <xsl:attribute name="sFile_xml">
		<xsl:value-of select="boolean(document($hit/sf))"/>
	      </xsl:attribute>
	      <xsl:attribute name="tFile_xml">
		<xsl:value-of select="boolean(document($hit/tf))"/>
	      </xsl:attribute>
	      <xsl:if test="not(boolean(document($hit/tf)))">
		
		<xsl:variable name="orig_path" select="$current_pf_loc"/>
		<xsl:variable name="orig_name" select="substring($current_pf_name, 1, string-length($current_pf_name) - 4)"/>

		<xsl:attribute name="tFile_orig">
		  <xsl:value-of select="concat($orig_path, $orig_name)"/>
		</xsl:attribute>
		
	      </xsl:if>
	    </xsl:if>
	    <hit>
	      <xsl:copy-of select="$hit"/>
	    </hit>
	    <xsl:if test="not($hit = $dit)">
	      <dit>
		<xsl:copy-of select="$dit"/>
	      </dit>
	    </xsl:if>
	  </file>
	</xsl:for-each>
      </parallel_files>
    </xsl:variable>
    
    <xsl:result-document href="{$outDir}/{concat($slang, '2', $tlang)}_{$outFile}.{$e}" format="{$outFormat}">
      <parallel_files>
	<xsl:attribute name="check_direction">
	  <xsl:value-of select="concat($slang, '2', $tlang)"/>
	</xsl:attribute>
	<xsl:attribute name="ok_counter">
	  <xsl:value-of select="count($para_files/parallel_files/file[./@parallelity = 'true'] )"/>
	</xsl:attribute>
	<xsl:attribute name="not_ok_counter">
	  <xsl:value-of select="count($para_files/parallel_files/file[./@parallelity = 'false'] )"/>
	</xsl:attribute>
	<xsl:copy-of select="$para_files/parallel_files/*"/>
      </parallel_files>
    </xsl:result-document>
    
  </xsl:template>

</xsl:stylesheet>

